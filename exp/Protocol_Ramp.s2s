' Protocol to run a ramp experiment.

#include "Gen_Functions.s2s"

' User inputs - motor motion
var lmotor0 := 1.0; ' [mm] motor position at the start

' User inputs - stimulation
var tStimOn := 0.5; ' [s] time of muscle stimulation onset (relative to the time of start of one cycle)
var tStimOff := 0.7; ' [s] time of muscle stimulation offset (relative to the time of start of one cycle)
var stimCF := 100.0; ' [Hz] muscle stimulation frequency

var vRamp := -2.0;
var durIso := tStimOn; ' [s] duration of isometric parts
var durRamp := (tStimOff-tStimOn)+0.4; ' [s] duration of ramp 

' Initialize signal, time etc.
var nstep% := 10000; ' number of steps per second (i.e. dt = 1/nstep%)
var nsamp% := round(nstep%*(2*durIso+durRamp)); ' number of samples of our signal
var time[nsamp%], lmotor[nsamp%], stim[nsamp%], sig[nsamp%][2]; ' declare time and lmotor variable

' Make time & motor position signal
ArrConst(time[], 1.0/nstep%); time[0] := 0; ArrIntgl(time[]); ' [s] time-axis
MakeRamp(time[],lmotor[],lmotor0,durIso,durRamp,vRamp);
MakeStimulation(time,stim,tStimOn,tStimOff,stimCF);
ArrDiv(lmotor,coef); ' From motor position in millimeters to motor position in voltage
TestMotorMotion(lmotor[],DACscale); ' Test whether motor motion is feasible/can be realised

' Make signal sent to both DAC outputs
ArrAdd(sig[][0],lmotor[]); 
ArrAdd(sig[][1],stim[]);
CorrectSignal(sig[][],DACscale); ' Correct motor position signal depending on DAC range

' Add to play waveform configuration
var dacs%[2]; dacs%[0] := 0; dacs%[1] := 1;  'list of dacs
PlayWaveAdd("X", "SineWave", dacs%, nstep%, sig[][]); '
PlayWaveCycles("X",1);

' Open new datafile and start sampling!
BringToPosition(sig[0][0]*DACscale/5); ' Bring motor to starting position
FileNew(0, 1); ' Open new datafile
SampleStart(0); ' Start sampling
while SampleStatus() <> 2 do Yield() wend; ' Wait until samples has (really) started
SampleKey("X");
while PlayWaveStatus$() = "X" do Yield() wend; ' Wait until waveform has been played
SampleStop(); ' Stop sampling
FileSave();
BringToPosition(LmotorRest); ' Bring motor to rest position
FileClose(-1,-1);  ' Close the file and do not save it

' Functions
func MakeStimulation(time[],stim[],tStimOn,tStimOff,stimCF)
' MakeStimStimulation calculates the stimulation over time.

' Inputs:
    ' time      = the time-axis [s]
    ' stim      = an empty array in which the stimulation over time will be placed
    ' tStimOn   = time onset of stimulation [s]
    ' tStimOff  = time offset of stimulation [s]
    ' stimCF    = stimulation frequency [Hz]
' Outputs:
    ' stim      = the stimulation over time

    
    var tBlock := 1.0/stimCF; ' [s] duration between two stimulation pulses
    ArrConst(stim[], 0.0); 
    var idx%;
    for idx%:=0 to Len(time[])-1 do 
        if time[idx%]>=tStimOn and time[idx%]<=tStimOff+0.5*tBlock then
            if ((time[idx%]-tStimOn) mod tBlock) <= tBlock/2 then
                stim[idx%] := 10;
            else
                stim[idx%] := 0;
            endif;
        endif;
    next;
return 1;
end;

func MakeRamp(time[],lmotor[],lmotor0,durIso,durRamp,vRamp)
' MakeRamp makes a ramp trajectory for the motor position over time.

' Inputs:
    ' time      = the time-axis [s]
    ' lmotor    = an empty array in which the motor position over time will be placed [mm]
    ' lmotor0   = the motor position at the start [mm]
    ' durIso    = the duration of the first isometric phase [s]
    ' durRamp   = the duration of the ramp (constant change in motor position) [s]
' Outputs:
    ' vRamp    	= the motor velocity during the ramp [mm/s]

    ' Define time points of this experiments
    var tP1 := durIso; ' [s] time-point at which the first isometric phase ends
    var tP2 := tP1+durRamp; ' [s] time-point at which the ramp ends

    ' Now make the signal input to the motor.
    var idx%;
    for idx%:=0 to Len(time[])-1 do 
        if time[idx%] <= tP1 then
            lmotor[idx%] := lmotor0;
        endif;
        if time[idx%]  > tP1 and time[idx%] <tP2 then
            lmotor[idx%] := lmotor0+(time[idx%]-tP1)*vRamp;
        endif;
        if time[idx%] > tP2 then
            lmotor[idx%] := lmotor0+durRamp*vRamp;
        endif;
    next;
return 1;
end;